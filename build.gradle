///////////////////////////////////////////////////////////////////////////////
// Build Script for building aggregated JavaPOS library from 
// JavaPOS Contracts, JavaPOS Controls, and JCL libraries
// 
// Author: denis.kuniss@wincor-nixdorf.com
///////////////////////////////////////////////////////////////////////////////

plugins {
    id 'com.gradle.build-scan' version '1.9'
	id 'java'
	id 'eclipse'
//  id 'maven' // comment this in and the next line out for populating to a local Maven repository for testing purposes
    id 'com.github.oehme.sobula.bintray-release' version '0.6.7'
}

ext.githubProjectUrl = 'https://github.com/JavaPOSWorkingGroup/javapos'
ext.travisProjectUrl = 'https://travis-ci.org/JavaPOSWorkingGroup/javapos'

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'

	apply from: 'buildScan.gradle'
}

task wrapper(type: Wrapper) {
	gradleVersion = '5.6.1'
}


///////////////////////////////////////////////////////////////////////////////
// Names and Versions
///////////////////////////////////////////////////////////////////////////////

def artifactName = 'javapos'
def groupId='org.javapos'
def uposVersion = '1.14'
version="${uposVersion}.2" // the last part after dot is the build/release version

// dependency versions
def javaposContractsVersion = "$uposVersion.3"
def javaposControlsVersion  = "$uposVersion.1"
def jclVersion = '2.3.1'


///////////////////////////////////////////////////////////////////////////////
// Build Dependencies
///////////////////////////////////////////////////////////////////////////////

repositories {
    jcenter()
    if (project.plugins.hasPlugin('maven')) 
    { // evaluates only at development environment, not at travis-ci.org
	    maven {
	        url "file:$localTestFileRepositoryDir"
	    }
	}
}

configurations {
	javaposProjects
	javaposSources
}

dependencies {
	javaposProjects ("org.javapos:javapos-contracts:$javaposContractsVersion") { transitive = false } 
	javaposProjects ("org.javapos:javapos-controls:$javaposControlsVersion") { transitive = false }
	javaposProjects ("org.javapos:javapos-config-loader:$jclVersion") { transitive = false }
	
	javaposSources ("org.javapos:javapos-contracts:$javaposContractsVersion:sources") { transitive = false } 
	javaposSources ("org.javapos:javapos-controls:$javaposControlsVersion:sources") { transitive = false }
	javaposSources ("org.javapos:javapos-config-loader:$jclVersion:sources") { transitive = false }
}


///////////////////////////////////////////////////////////////////////////////
// Build Tasks
///////////////////////////////////////////////////////////////////////////////

sourceCompatibility = '1.5' // this is only for having a common JDK description across any development machine; we do not compile in this project

// this is to request the best matching JDK (currently 1.5) for building jars in Eclipse 
// it is recommended to install a JDK which matches best to avoid applying APIs methods and types not available in specified JVM compatibility version  
eclipse.classpath {
	containers.clear()
	containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/J2SE-$sourceCompatibility"
}

def javaposManifest = 	manifest {
        attributes('Specification-Title': 'UnifiedPOS Standard',
				   'Specification-Vendor': 'UnifiedPOS Committee',
                   'Specification-Version': uposVersion,
                   'Implementation-Title': 'JavaPOS Library (Contracts, Controls, JCL)',
				   'Implementation-Vendor': 'github.com/JavaPOSWorkingGroup',
                   'Implementation-Version': version)
}

jar {
	baseName = artifactName
	manifest = javaposManifest

	inputs.property 'version', version
	inputs.property 'uposVersion', uposVersion
    
    duplicatesStrategy = DuplicatesStrategy.FAIL
	configurations.javaposProjects.each {
		from(zipTree(it)) {
			exclude 'META-INF/**'
		}
	}
}

sourceJar {
	manifest = javaposManifest
    duplicatesStrategy = DuplicatesStrategy.FAIL
	configurations.javaposSources.each {
		from(zipTree(it)) {
			exclude 'META-INF/**'
		}
	}
}


///////////////////////////////////////////////////////////////////////////////
// Artifact Upload
///////////////////////////////////////////////////////////////////////////////

if (project.plugins.hasPlugin('com.github.oehme.sobula.bintray-release')) 
{ // evaluates only on travis-ci.org for releasing

    group = "org.javapos"
    description = "JavaPOS Library"
     
    contacts {
        "builder@javapos.org" {
            moniker "JavaPOS Working Group"
            roles "owner"
            github "JavaPOSWorkingGroup"
        }
    }
    
    bintray.user = "javaposworkinggroup"
    bintray.pkg.version.mavenCentralSync.user = "kuniss"
}

if (project.plugins.hasPlugin('maven')) 
{ // evaluates only at development environment, not at travis-ci.org

	artifacts {
		archives jar
	}
	
	uploadArchives {
	    repositories {
	        mavenDeployer {
	            repository(url: "file:$localTestFileRepositoryDir")
	            pom.artifactId = artifactName
	            pom.groupId = groupId
	            pom.version = version
	        }
	    }
	}

}